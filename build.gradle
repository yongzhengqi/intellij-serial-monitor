buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.2.16'
}

apply plugin: 'java'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:deprecation"
}

// take the version number defined in 'gradle.properties' and use that in plugin.xml
task initConfig(type: Copy) {
    from('src/main/resources') {
        include '**/plugin.xml'
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: version])
    }
}

dependencies {
    compile files('lib/jssc-2.8.0.jar')
}

apply plugin: 'idea'
idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}

apply plugin: 'org.jetbrains.intellij'
intellij {
    version ideaVersion
    pluginName 'intellij-serial-monitor'
    updateSinceUntilBuild false

    // SET YOUR JETBRAINS LOGIN DETAILS AS ENVIRONMENT VARIABLES SO THAT YOU CAN PUBLISH A NEW BUILD TO THE REPOSITORY
    publishPlugin {
        username System.getenv('JETBRAINS_USERNAME')
        password System.getenv('JETBRAINS_PASSWORD')
    }
}

version = "${version}"